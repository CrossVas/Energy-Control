plugins {
    id 'java'
    id 'net.minecraftforge.gradle' version '4.1.12'
    id 'de.undercouch.download' version '4.0.4'
    id 'wtf.gofancy.fancygradle' version '1.0.0'
}

version = "$VERSION_MC-$VERSION_MAJOR.$VERSION_MINOR.$VERSION_PATCH"
group = "com.zuxelus.energycontrol"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    
    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                EnergyControl {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                EnergyControl {
                    source sourceSets.main
                }
            }
        }
    }
}

fancyGradle {
    patches {
        patch(
                wtf.gofancy.fancygradle.patch.Patch.RESOURCES, 
                wtf.gofancy.fancygradle.patch.Patch.COREMODS, 
                wtf.gofancy.fancygradle.patch.Patch.CODE_CHICKEN_LIB, 
                wtf.gofancy.fancygradle.patch.Patch.ASM
        )
    }
}

def downloadDir = "$buildDir/download"

task downloadGalacticraft {
    download {
        src([
                'https://micdoodle8.com/new-builds/GC-1.12-dev/261/GalacticraftCore-Dev-1.12.2-4.0.2.261.jar',
                'https://micdoodle8.com/new-builds/GC-1.12-dev/261/Galacticraft-Planets-Dev-1.12.2-4.0.2.261.jar',
                'https://micdoodle8.com/new-builds/GC-1.12-dev/261/MicdoodleCore-Dev-1.12.2-4.0.2.261.jar'
        ])
        mkdir downloadDir
        dest downloadDir
        overwrite false
    }
}

repositories {
    maven {
        name 'CurseMaven'
        url "https://www.cursemaven.com"
    }
    maven {
        name 'ic2'
        url 'https://maven.ic2.player.to/'
    }
    maven {
        name 'Progwml6 maven'
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        name 'CoFH Maven'
        url 'https://maven.covers1624.net'
    }
    maven {
        name 'Modmuss maven'
        url 'https://maven.modmuss50.me/'
    }
    maven {
        name 'ComputerCraft'
        url 'https://squiddev.cc/maven/'
    }
    maven {
        name 'BuildCraft'
        url 'https://mod-buildcraft.com/maven'
    }
    maven {
        name 'OpenComputers'
        url 'https://maven.cil.li/'
    }
    flatDir {
        name 'Downloads'
        dir downloadDir
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: '1.12.2-14.23.5.2855'
    
    compileOnly group: 'net.industrial-craft', name: 'industrialcraft-2', version: "$VERSION_IC2-ex112", classifier: 'dev'
    compileOnly fg.deobf(group: 'RebornCore', name: 'RebornCore-1.12.2', version: VERSION_REBORN_CORE, classifier: 'universal') {
        transitive false
    }
    compileOnly fg.deobf(group: 'TechReborn', name: 'TechReborn-1.12.2', version: VERSION_TR, classifier: 'universal') {
        transitive false
    }
    compileOnly fg.deobf(group: 'TechReborn', name: "TechReborn-ModCompatibility-1.12.2", version: VERSION_TR_COMPAT, classifier: 'universal')
    compileOnly fg.deobf(group: 'org.squiddev', name: "cc-tweaked-1.12.2", version: VERSION_COMPUTERCRAFT)
    compileOnly fg.deobf(group: 'com.mod-buildcraft', name: "buildcraft-api", version: VERSION_BUILDCRAFT)
    compileOnly fg.deobf(group: 'com.mod-buildcraft', name: "buildcraft-main", version: VERSION_BUILDCRAFT)
    compileOnly (group: 'com.brandon3055.draconicevolution', name: "Draconic-Evolution", version: VERSION_DRACONIC_EVOLUTION, classifier: 'deobf') {
        transitive false
    }
    compileOnly (group: 'com.brandon3055.brandonscore', name: "BrandonsCore", version: VERSION_BRANDONSCORE, classifier: 'deobf') {
        transitive false
    }
    compileOnly fg.deobf(group: 'li.cil.oc', name: "OpenComputers", version: VERSION_OPENCOMPUTERS)
    compileOnly fg.deobf(group: 'mezz.jei', name: "jei_1.12.2", version: VERSION_JEI)
    compileOnly fg.deobf(group: 'cofh', name: "RedstoneFlux", version: "1.12-$VERSION_RF", classifier: 'universal')
    compileOnly fg.deobf(group: 'cofh', name: "CoFHCore", version: "1.12.2-$VERSION_COFHCORE", classifier: 'universal') {
        exclude group: 'mezz.jei'
    }
    compileOnly fg.deobf(group: 'cofh', name: "CoFHWorld", version: "1.12.2-$VERSION_COFHWORLD", classifier: 'universal')
    compileOnly fg.deobf(group: 'cofh', name: "ThermalFoundation", version: "1.12.2-$VERSION_THERMALFOUNDATION", classifier: 'universal')
    compileOnly fg.deobf(group: 'codechicken', name: "CodeChickenLib", version: "1.12.2-$VERSION_CODECHICKENLIB", classifier: 'universal')
    compileOnly fg.deobf(group: 'cofh', name: "ThermalExpansion", version: "1.12.2-$VERSION_THERMALEXPANSION", classifier: 'universal') {
        exclude group: 'mezz.jei'
    }
    compileOnly group: 'micdoodle8.mods.galacticraft', name: "GalacticraftCore-Dev", version: VERSION_GALACTICRAFT
    compileOnly group: 'micdoodle8.mods.galacticraft', name: "Galacticraft-Planets-Dev", version: VERSION_GALACTICRAFT
    compileOnly group: 'micdoodle8.mods.galacticraft', name: "MicdoodleCore-Dev", version: VERSION_GALACTICRAFT
    compileOnly fg.deobf(group: 'curse.maven', name: "applied-energistics-2-223794", version: VERSION_AE2)
    compileOnly fg.deobf(group: 'curse.maven', name: "mekanism-268560", version: VERSION_MEKANISM)
    compileOnly fg.deobf(group: 'curse.maven', name: "mekanism-generators-268566", version: VERSION_MEKANISM_GENERATORS)
    compileOnly fg.deobf(group: 'curse.maven', name: "extreme-reactors-250277", version: VERSION_EXTREME_REACTORS)
    compileOnly fg.deobf(group: 'curse.maven', name: "zerocore-247921", version: VERSION_ZEROCORE)
    compileOnly fg.deobf(group: 'curse.maven', name: "nuclearcraft-mod-226254", version: VERSION_NUCLEARCRAFT)
    compileOnly fg.deobf(group: 'curse.maven', name: "ic2-classic-3093608", version: VERSION_IC2_CLASSIC)
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filesMatching("mcmod.info") {
            expand 'version':project.version, 'mcversion': '1.12.2'
        }
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'com/zuxelus/energycontrol/api/**'
    archiveClassifier.set('api')
}

artifacts {
    archives apiJar
}
